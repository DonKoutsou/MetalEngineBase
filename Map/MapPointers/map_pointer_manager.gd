extends Node2D

#This script manages all the Ship and Town markers. 
class_name MapPointerManager

@export var CircleDr : CircleDrawer

@export var MapLonePos : Node2D
@export var MarkerScene : PackedScene
@export var MapSpotMarkerScene : PackedScene
@export var OrderMarkerScene : PackedScene
@export var FriendlyColor : Color
@export var EnemyColor : Color
@export var ConvoyColor : Color
@export var UIEventH : UIEventHandler
@export var ControllerEventHandler : ShipControllerEventHandler
@export var MapSpotMarkerParent : Node2D
@export var ShipMarkerParent : Node2D
@export var OrderMarkersParent : Node2D

#@export var SpotColor : Color

var Ships : Array[Node2D] = []
var _ShipMarkers : Array[ShipMarker] = []
var Spots : Array[Node2D] = []
var _SpotMarkers : Array[SpotMarker] = []

var _OrderMarkers : Array[OrderMarker] = []

static func GetInstance() -> MapPointerManager:
	return Instance

static var Instance : MapPointerManager

var ControlledShip : MapShip

signal TargetSelected(Ship : MapShip)
signal TargetSpotSelected(Target : SpotMarker)

func _enter_tree() -> void:
	Instance = self

func _ready() -> void:
	UIEventH.connect("MarkerEditorCleared", ClearLines)
	ControllerEventHandler.connect("OnControlledShipChanged", OnControlledShipChanged)
	ControlledShip = ControllerEventHandler.CurrentControlled

func UpdateCameraZoom(NewZoom : float) -> void:
	CircleDr.UpdateCameraZoom(NewZoom)

func UpdateCameraPosition(_NewPos : Vector2) -> void:
	pass
	#CircleDr.queue_redraw()

func OnControlledShipChanged(Ship : MapShip) -> void:
	ControlledShip = Ship

func OnShipTargetSelected(Marker : ShipMarker) -> void:
	var index = _ShipMarkers.find(Marker)
	var Ship = Ships[index]
	TargetSelected.emit(Ship)

func OnSpotTargetSelected(Marker : SpotMarker) -> void:
	TargetSpotSelected.emit(Marker)

func ClearLines() -> void:
	for g in $MapLines.get_children():
		g.queue_free()
	PopUpManager.GetInstance().DoFadeNotif("Marker Cleared")
	
func AddOrder(Order : Resource) -> void:
	print("Added Order")
	var marker = OrderMarkerScene.instantiate() as OrderMarker
	OrderMarkersParent.add_child(marker)
	marker.SetOrder(Order)
	_OrderMarkers.append(marker)

func RemoveOrder(Order : Resource) -> void:
	print("Removed Order")
	for g in _OrderMarkers:
		if (g.Order == Order):
			g.queue_free()
			_OrderMarkers.erase(g)
			break

func AddShip(Ship : Node2D, Friend : bool, notify : bool = false) -> ShipMarker:
	if (Ships.has(Ship)):
		if (Ship is MapShip):
			if (!Ship.Friendly() and !Ship.Destroyed and notify):
				if (Ship.Convoy):
					_ShipMarkers[Ships.find(Ship)].PlayHostileShipNotif("Convoy\nLocated")
				else:
					_ShipMarkers[Ships.find(Ship)].PlayHostileShipNotif("Hostile Ship\nLocated")
		return
	
	Ship.tree_exited.connect(RemoveShip)
	Ships.append(Ship)
	var marker = MarkerScene.instantiate() as ShipMarker

	ShipMarkerParent.add_child(marker)
	_ShipMarkers.append(marker)
	
	marker.global_position = Ship.global_position
	
	if (Friend):
		marker.modulate = FriendlyColor
	else:
		marker.modulate = EnemyColor
		#_ShipMarkers[Ships.find(Ship)].PlayHostileShipNotif()
	
	if (Ship is MapShip):
		if (!Ship.Friendly()):
			if (Ship.Convoy):
				marker.modulate = ConvoyColor
			
			marker.ShipTargetSelected.connect(OnShipTargetSelected)
			
			if (!Ship.Destroyed and notify):
				marker.PlayHostileShipNotif("Hostile Ship\nLocated")
		else :
			marker.ShipTargetSelected.connect(OnShipTargetSelected)
			marker.ShipSelected.connect(ControllerEventHandler.ShipChanged.bind(Ship))
			Ship.Crosswind.connect(marker.PlayCounterWindNotif)
		
	marker.Init(Ship)
	
	return marker

func AddSpot(Spot : MapSpot, PlayAnim : bool) -> void:
	if (Spots.has(Spot)):
		return
	Spots.append(Spot)
	var marker = MapSpotMarkerScene.instantiate() as SpotMarker
	marker.TownTargetSelected.connect(OnSpotTargetSelected)
	MapSpotMarkerParent.add_child(marker)
	
	_SpotMarkers.append(marker)
	marker.SetMarkerDetails(Spot, PlayAnim)
	
	if (Spot.AlarmRaised):
		marker.OnAlarmRaised(false)
	else:
		Spot.SpotAlarmRaised.connect(marker.OnAlarmRaised)

	marker.global_position = Spot.global_position
	
func RemoveShip(Ship : Node2D) -> void:
	if (!Ships.has(Ship)):
		return
	
	Ship.tree_exited.disconnect(RemoveShip)
	var index = Ships.find(Ship)
	_ShipMarkers[index].queue_free()
	_ShipMarkers.remove_at(index)
	Ships.remove_at(index)

func FixMarkerClipping() -> void:
	var ShipMarkers = get_tree().get_nodes_in_group("MapShipVizualiser")
	for Marker1 : TextureRect in ShipMarkers:
		if (!Marker1.is_visible_in_tree()):
			continue

		for Marker2 : TextureRect in ShipMarkers:
			if (Marker1 == Marker2):
				continue
			if (!Marker2.is_visible_in_tree()):
				continue
			
			var OffsetToApply : float = 4 * Marker1.scale.x
			if (Marker2.global_position.x < Marker1.global_position.x):
				OffsetToApply *= -1
			var tries = 0
			while (Marker1.get_global_rect().intersects(Marker2.get_global_rect()) and tries < 10):
				Marker2.owner.position.x += OffsetToApply
				tries += 1

func FixLabelClipping() -> void:
	var Mapinfos = get_tree().get_nodes_in_group("MapInfo")
	var AllMapInfos = get_tree().get_nodes_in_group("UnmovableMapInfo")
	AllMapInfos.append_array(Mapinfos)
	for Info1 : Control in Mapinfos:
		if (!Info1.is_visible_in_tree()):
			continue

		var r1 = Info1.get_global_rect()
		for Info2 : Control in AllMapInfos:
			if (Info1 == Info2):
				continue
			if (!Info2.is_visible_in_tree()):
				continue

			var r2 = Info2.get_global_rect()
			var tries = 0
			while (r1.intersects(r2) and tries < 20):
				Info1.owner.UpdateSignRotation()
				tries += 1

var hulls: Array[PackedVector2Array] = []

func _process(delta: float) -> void:
	hulls.clear()
	
	var CamPos = ShipCamera.GetInstance().get_screen_center_position()
	var d = delta
	#if (SimulationManager.IsPaused()):
		#d = 0
	#var LightAmm = WeatherManage.GetLightAmm()
	for g in _ShipMarkers.size():
		var Ship = Ships[g]
		if (Ship is MapShip):
			if (Ship.Friendly() and Ship.Command == null):
				hulls.append(Ship.GetBiggestRadarCicle())
			#var visibility = WeatherManage.GetVisibilityInPosition(Ship.global_position, LightAmm)
			#var Radius : float
			#if (Ship.RadarWorking):
				#Radius = max(Ship.Cpt.GetStatFinalValue(STAT_CONST.STATS.VISUAL_RANGE), 110 * visibility)
			#else:
				#Radius = 110 * visibility
		
		
		_ShipMarkers[g].Update(Ship == ControlledShip, CamPos, d)
	CircleDr.UpdatePolygons(hulls)
	FixLabelClipping()
	FixMarkerClipping()

func GetSaveData() -> SaveData:
	var Dat = SaveData.new()
	Dat.DataName = "Markers"
	for g in _ShipMarkers.size():
		
		var ship = Ships[g]
		var Marker = _ShipMarkers[g]
		
		if (ship is MapShip):
			if (!ship.Friendly()):
				if (!ship.Destroyed and ship.VisibleBy.size() == 0):
					Dat.Datas.append(Marker.GetSaveData())
	return Dat

func LoadSaveData(Data : SaveData) -> void:
	var Enemies = get_tree().get_nodes_in_group("Enemy")
	for D : SD_ShipMarker in Data.Datas:
		var SavedName = D.ShipName
		for S : MapShip in Enemies:
			var Name = S.GetShipName()
			if (SavedName == Name):
				var marker = AddShip(S, false, false)
				marker.TimeLastSeen = D.TimeLastSeen
				marker.global_position = D.Pos
				marker.UpdateTrajectory(D.Trajectory)
