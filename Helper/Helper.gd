extends Node

class_name Helper

@export var RegionColors : Dictionary[MapSpotCompleteInfo.REGIONS, Color]
@export var Cog : TextureRect

##Queue of files being loaded, we store them so we can signal those waiting for them that they are loaded
static var fileQueue : Dictionary[String, SignalObject]

static var Instance : Helper

func _ready() -> void:
	Instance = self
	set_physics_process(false)
	$CanvasLayer.visible = false
	Cog.pivot_offset = Cog.size / 2

func _exit_tree() -> void:
	fileQueue.clear()

#we chack every frame if any of the files has been loaded and remove it from the queue
func _process(_delta: float) -> void:
	var keys = fileQueue.keys()
	for fileIndex in range(fileQueue.size() - 1, -1, -1):
		var file : String = keys[fileIndex]
		var Status = ResourceLoader.load_threaded_get_status(file)
		if (Status == ResourceLoader.ThreadLoadStatus.THREAD_LOAD_LOADED):
			_LoadFinished(fileQueue[file], ResourceLoader.load_threaded_get(file))
			fileQueue.erase(file)
		if (Status == ResourceLoader.ThreadLoadStatus.THREAD_LOAD_FAILED):
			_LoadFinished(fileQueue[file], null)
			fileQueue.erase(file)

func _physics_process(delta: float) -> void:
	Cog.rotation = wrap(Cog.rotation + delta, 0, PI * 4)

static func LoadThreaded(File : String) -> SignalObject:
	#In case we are already loading file, return that
	if (fileQueue.has(File)):
		return fileQueue[File]
		
	var Sign = SignalObject.new()
	
	ResourceLoader.load_threaded_request(File, "", false, ResourceLoader.CACHE_MODE_REUSE)
	
	fileQueue[File] = Sign
	
	Instance.get_node("CanvasLayer").visible = true
	Instance.set_physics_process(true)
	
	return Sign

static func Translate(text: String, context: StringName = &"") -> String:
	var regex := RegEx.new()
	regex.compile(r"\[\[([A-Za-z0-9_.:-]+)\]\]")

	var result := ""
	var last_pos := 0

	for match in regex.search_all(text):
		var start := match.get_start()
		var end := match.get_end()

		result += text.substr(last_pos, start - last_pos)

		var key := match.get_string(1)
		var translated := String(TranslationServer.translate(key, context))

		result += translated
		last_pos = end

	result += text.substr(last_pos)
	return result

static func FromMinutesToString(Minutees : float) -> String:
	var Str : String = ""
	var Hours = 0
	var Mins = Minutees
	while Mins > 59:
		Hours += 1
		Mins -= 60
	if (Hours > 0):
		Str += "{0} hour(s) ".format([Hours])
	if (Mins > 0):
		Str += "{0} minute(s) ".format([roundi(Mins)])
	return Str

static func FromMinutesToStringShort(Minutees : float) -> String:
	var Str : String = ""
	var Hours = 0
	var Mins = Minutees
	while Mins > 59:
		Hours += 1
		Mins -= 60
	if (Hours > 0):
		Str += "{0} h(s) ".format([Hours])
	if (Mins > 0):
		Str += "{0} m(s) ".format([roundi(Mins)])
	return Str


static func array_to_curve(input : Array, dist : float) -> Curve2D:
	#dist determines length of controls, set dist = 0 for no smoothing
	var curve = Curve2D.new()

	#calculate first point
	var start_dir = input[0].direction_to(input[1])
	curve.add_point(input[0], - start_dir * dist, start_dir * dist)

	#calculate middle points
	for i in range(1, input.size() - 1):
		var prev : Vector2 = input[i-1]
		var dir = prev.direction_to(input[i+1])
		curve.add_point(input[i], -dir * dist, dir * dist)

	#calculate last point
	var end_dir = input[-1].direction_to(input[-2])
	curve.add_point(input[-1], - end_dir * dist, end_dir * dist)
	curve.bake_interval = 1
	return curve

#called internally to notify of loading ending
func _LoadFinished(Sign : SignalObject, File : Object) -> void:
	Sign.Sign.emit(File)
	$CanvasLayer.visible = false
	set_physics_process(false)
	Sign.unreference()

static func CallLater(Call : Callable, t : float = 1) -> void:
	await Instance.get_tree().create_timer(t).timeout
	if (Call.is_valid()):
		Call.call()

static func wait(secs : float) -> Signal:
	return Instance.get_tree().create_timer(secs).timeout

static func mapvalue(val : float, minimum : float, maximum : float) -> float:
	return minimum + (maximum - minimum) * val

static func normalize_value(value: float, minimum: float, maximum: float) -> float:
	return (value - minimum) / (maximum - minimum)

static func AngleToDirection(angle: float) -> String:
	var directions = ["East", "Southeast",  "South", "Southwest", "West", "Northwest", "North","Northeast"]
	var index = int(fmod((angle + PI/8 + TAU), TAU) / (PI / 4)) % 8
	return directions[index]

static func AngleToDirectionShort(angle: float) -> String:
	var directions = ["E", "SE",  "S", "SW", "W", "NW", "N","NE"]
	var index = int(fmod((angle + PI/8 + TAU), TAU) / (PI / 4)) % 8
	return directions[index]

static func is_even(number: int) -> bool:
	return number % 2 == 0

static func mapf(value: float, in_min: float, in_max: float, out_min: float, out_max: float) -> float:
	return (value - in_min) * (out_max - out_min) / (in_max - in_min) + out_min

static func mapv2(value: Vector2,in_min: Vector2,in_max: Vector2,out_min: Vector2, out_max: Vector2) -> Vector2:
	return Vector2(mapf(value.x, in_min.x, in_max.x, out_min.x, out_max.x),mapf(value.y, in_min.y, in_max.y, out_min.y, out_max.y))

static func angle_difference_radians(angle1: float, angle2: float) -> float:

	# Calculate the difference
	var difference = angle2 - angle1

	# Adjust for wrap around
	if difference > PI:
		difference -= TAU
	elif difference < -PI:
		difference += TAU

	return difference

static func SmoothLine(L : Array, res : float = 200) -> Array[Vector2]:
	var newline : Array[Vector2]
	for pointIndex in range(0, L.size()-1):
		var currentpoint = L[pointIndex] as Vector2
		newline.append(currentpoint)
		var nextpoint = L[pointIndex + 1]
		var dist = currentpoint.distance_to(nextpoint)
		var direction = currentpoint.direction_to(nextpoint)
		if (currentpoint.y == nextpoint.y):
			continue
		for g in range(1, dist / res):
			
			var newpoint = currentpoint + (direction * (res * g))
			
			#var newdist = abs(currentpoint.y - newpoint.y)
			#var ydist = abs(currentpoint.y - nextpoint.y)
			
			#var d = newdist / ydist
			var s = smoothstep(currentpoint.y, nextpoint.y, newpoint.y)
			newpoint.y = lerp(currentpoint.y, nextpoint.y, s)
			newline.append(newpoint)
	
	newline.append(L[L.size() - 1])
	
	return newline

static func UpDownLerp(Max : float, t: float) -> float:
	# If t <= 0.5, interpolate from 0 to midpoint
	if t * 2 <= Max:
		return lerp(0.0, 1.0, t / (Max / 2))  # Scale t for the first half (0 to 1)
	else:
		# If t > 0.5, interpolate from midpoint to 0
		return lerp(1.0, 0.0, t / Max)  # Scale t for the first half (0 to 1)

static func SmoothLine2(L : Array, res : float = 200) -> Array[Vector2]:
	var newline : Array[Vector2]
	for pointIndex in range(0, L.size()-1):
		var currentpoint = L[pointIndex]
		newline.append(currentpoint)
		var nextpoint = L[pointIndex + 1]
		var dist = currentpoint.distance_to(nextpoint)
		var direction = currentpoint.direction_to(nextpoint)
		if (currentpoint.y == nextpoint.y):
			continue
		for g in range(1, dist / res ):
			
			var newpoint = currentpoint + (direction * (res * g))
			
			#var newdist = abs(currentpoint.y - newpoint.y)
			#var ydist = abs(currentpoint.y - nextpoint.y)
			
			#var d = newdist / ydist
			var s = smoothstep(currentpoint.y, nextpoint.y, newpoint.y)
			var s2 = smoothstep(currentpoint.x, nextpoint.x, newpoint.x)
			newpoint.y = lerp(currentpoint.y, nextpoint.y, s)
			newpoint.x = lerp(currentpoint.x, nextpoint.x, s2)
			newline.append(newpoint)
	
	newline.append(L[L.size() - 1])
	
	return newline

static func DistanceToDistance(Dist: float) -> String:
	if Dist > 8000:
		return "very far"
	elif Dist > 5000:
		return "far"
	elif Dist > 3000:
		return "close"
	elif Dist > 1000:
		return "fairly close"
	else:
		return "very close"
		
static func GetCityByName(CityName : String) -> MapSpot:
	var SpotGroups = ["CAPITAL", "CITY_CENTER", "VILLAGE"]
	var cities = []
	for g in SpotGroups:
		cities.append_array(Instance.get_tree().get_nodes_in_group(g))
	var CorrectCity : MapSpot
	for g in cities:
		var cit = g as MapSpot
		if (cit.GetSpotName() == CityName):
			CorrectCity = cit
			break
	return CorrectCity

static func GetClosestSpot(Pos : Vector2) -> MapSpot:
	var Closest : MapSpot
	var Dist = 99999999999999999
	for g in Instance.get_tree().get_nodes_in_group("City"):
		var Dist2 = Pos.distance_squared_to(g.global_position)
		if (Dist2 < Dist):
			Dist = Dist2
			Closest = g
			if (Dist < 200):
				break

	return Closest

static func GetSpotsCloserThan(Pos : Vector2, DistSquared : float) -> Array[MapSpot]:
	var Spots : Array[MapSpot]
	for g in Instance.get_tree().get_nodes_in_group("City"):
		var Dist2 = Pos.distance_squared_to(g.global_position)
		if (Dist2 < DistSquared):
			Spots.append(g)

	return Spots

static func GetSpotByName(CityName : String) -> MapSpot:
	var CorrectCity : MapSpot
	for g in Instance.get_tree().get_nodes_in_group("City"):
		var cit = g as MapSpot
		if (cit.GetSpotName() == CityName):
			CorrectCity = cit
			break
	return CorrectCity

static func FindPath(start_city: String, end_city: String) -> Array:
	#var cities = get_tree().get_nodes_in_group("EnemyDestinations")
	var queue = []
	var visited = {}
	var parent = {}
	
	# Initialize the BFS
	queue.append(start_city)
	visited[start_city] = true
	parent[start_city] = null
	
	# Perform the BFS
	while queue.size() > 0:
		var current_city = queue.pop_front()
		
		# If we reached the end_city, reconstruct the path
		if current_city == end_city:
			return reconstruct_path(parent, end_city)
		
		# Explore neighboring cities
		var Cit = GetCityByName(current_city)
		if (Cit.NeighboringCities.size() == 0):
			printerr(Cit.GetSpotName + " has no neighboring cities. Seems sus.")
		for neighbor in Cit.NeighboringCities:
			if not visited.has(neighbor):
				queue.append(neighbor)
				visited[neighbor] = true
				parent[neighbor] = current_city
	
	# If no path is found, return an empty array
	print("Failed to find a path from " + start_city + " to " + end_city)
	return []

static func reconstruct_path(parent: Dictionary, end_city: String) -> Array:
	var path = []
	var current_city = end_city
	while current_city != null:
		path.append(current_city)
		current_city = parent[current_city]
		
	path.reverse()  # Reverse the path to get it from start to end
	return path

static func TrySetFire() -> bool:
	randomize()
	var random_value = randf()
	return random_value < 0.2

static func GetColorForRegion(R : MapSpotCompleteInfo.REGIONS):
	return Instance.RegionColors[R]

static func fuel_used_for_distance(dist: float, FuelNow: float, FuelEff: float, Weight: float) -> float:
	var eff_eff = FuelEff - (Weight / 40.0)
	var A = pow(FuelNow * eff_eff, 0.55)
	var arg = A - dist/50.0
	if arg <= 0:
		return FuelNow # not enough fuel: use what's left 
	var FuelAfter = pow(arg, 1.0/0.55) / eff_eff
	return FuelNow - FuelAfter

static func CombineNoiseAmplitude(noiseAmplitudes: Array[float]) -> float:
	var sumSq := 0.0
	for a in noiseAmplitudes:
		var aa = max(a, 0.0)
		sumSq += aa * aa
	return sqrt(sumSq)
