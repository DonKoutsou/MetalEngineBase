extends MapShip
#/////////////////////////////////////////////////////////////
#██   ██  ██████  ███████ ████████ ██ ██      ███████     ███████ ██   ██ ██ ██████  
#██   ██ ██    ██ ██         ██    ██ ██      ██          ██      ██   ██ ██ ██   ██ 
#███████ ██    ██ ███████    ██    ██ ██      █████       ███████ ███████ ██ ██████  
#██   ██ ██    ██      ██    ██    ██ ██      ██               ██ ██   ██ ██ ██      
#██   ██  ██████  ███████    ██    ██ ███████ ███████     ███████ ██   ██ ██ ██      
#/////////////////////////////////////////////////////////////
#Enemy class. Enemies that are patrolling are controlled from COMMANDER.
#If its not patrolling it needs to behavior as it is static and its just protecting city.
#/////////////////////////////////////////////////////////////
class_name HostileShip

#Direction this patrol will go towards if its a patrol
@export var Direction = -1
@export var ShipName : String
@export var Patrol : bool = true
@export var Convoy : bool = false
@export var BT : PackedScene

#This array will be filled by commander when this ship is sent after another ship
var PursuingShips : Array[PlayerDrivenShip]
#This value will be filled by commander when this ship is sent to investigate a position
var PositionToInvestigate : Vector2
#Spot that was chosen to stop and refuel
var RefuelSpot : MapSpot
#Spot that was chosen to hide until alarm goese of
var RefugeSpot : MapSpot
#Filled with player ships when they can see this ship
var VisibleBy : Array[Node2D]

#var LOD : int = 0
var Captured : bool
var BTree : BeehaveTree
var BBoard : Blackboard
var UseDefaultBehavior : bool = false

var LoadingSave : bool = false
var Spawned : bool = false

#if lodded it means it should not process
var currentLOD : int = 0
var Lodded : bool = true
#Until this value reaches a certain threashold ships speed or trajectory are not known
var ExposedValue : float = 0.0

var Reloading : float = 0

signal OnPlayerShipMet(PlayerSquad : Array[MapShip] , EnemySquad : Array[MapShip], Mis : Array[BattleShipStats])
signal OnDestinationReached(Ship : HostileShip)

signal OnPositionInvestigated(Pos : Vector2)

signal ShipSpawned
signal ShipWrecked
signal Alarmed

#var SavedParent : Node

func SetLOD(lod : int) -> void:
	currentLOD = lod
	if (lod < 2):
		ToggleLod(false)
	else:
		ToggleLod(true)

func ToggleLod(t : bool) -> void:
	if (Lodded == t):
		return
	Lodded = t
	if (!t):
		WeatherManage.RegisterShip(self)
	else:
		WeatherManage.UnregisterShip(self)
	
func  _ready() -> void:
	BodyShape.connect("area_entered", BodyEnteredBody)
	BodyShape.connect("area_exited", BodyLeftBody)
	Cpt.connect("ShipPartChanged", PartChanged)
	RadarShape.VisStat = Cpt._GetStat(STAT_CONST.STATS.VISUAL_RANGE)
	ElintShape.ElintStat = Cpt._GetStat(STAT_CONST.STATS.ELINT)
	
	#GetDock().SquadSonarRangeChanged.connect(OnSonarRangeChanged)
	GetDock().SquadElintRangeChanged.connect(OnElintRangeChanged)
	ElintShape.ElintRangeChanged.connect(OnElintRangeChanged)
	#SonarShape.AerosonarRangeChanged.connect(OnSonarRangeChanged)
	
	ToggleFuelRangeVisibility(false)
	call_deferred("InitialiseShip")
	
	#ENABLE FOR DEBUG PURPOSES
	if (Commander.ENEMY_DEBUG):
		MapPointerManager.GetInstance().AddShip(self, false)
	
	if (!Patrol and !Convoy):
		CurrentLandAltitude = TopographyMap.GetAltitudeAtGlobalPosition(global_position)
		UpdateAltitude(CurrentLandAltitude)
		RadarShape.Landed = true

func _exit_tree() -> void:
	WeatherManage.UnregisterShip(self)

func InitialiseShip() -> void:
	Spawned = true
	ShipSpawned.emit()
	
	if (!LoadingSave):
		for g in Cpt.CaptainStats:
			g.ForceMaxValue()
	
	if (Destroyed):
		Kill()
		return
	
	Commander.GetInstance().RegisterSelf(self)
	
	_UpdateShipIcon(ResourceLoader.load(Cpt.ShipIconFile))
	var ElintRange = Cpt.GetStatFinalValue(STAT_CONST.STATS.ELINT)
	if (ElintRange == 0):
		ElintShape.ToggleElint(false)
	else:
		ElintShape.UpdateELINTTRange()

	if (Patrol or Convoy):
		if (Command == null):
			FigureOutPath()
	else:
		SetSpeed(0)
		UseDefaultBehavior = true
	
	RadarShape.UpdateVizRange()
	#TogglePause(SimulationManager.IsPaused())

#Update function called by Commander class. Updating only ships that are not lodded
func _Update(delta: float) -> void:
	ElintShape.UpdateElint(delta)
	RadarShape.EvaluateRadarTargets(Altitude)
	var SimulationSpeed = SimulationManager.SimSpeed()
	
	if (Reloading > 0):
		#print("{0} is reloading a their missiles".format([Cpt.GetCaptainName()]))
		Reloading = max(0, Reloading - (delta * SimulationSpeed))
	
	for g in TrailLines:
		g.UpdateProjected(delta, 1)
	
	if (VisibleBy.size() > 0):
		ExposedValue += delta
	else:
		ExposedValue = max(0, ExposedValue - delta)
	
	if (UseDefaultBehavior):
		if (!Cpt.IsResourceFull(STAT_CONST.STATS.HULL)):
			Cpt.RefillResource(STAT_CONST.STATS.HULL ,0.02 * SimulationSpeed)
		
		if (!Cpt.IsResourceFull(STAT_CONST.STATS.MISSILE_SPACE)):
			Cpt.RefillResource(STAT_CONST.STATS.MISSILE_SPACE ,0.005 * SimulationSpeed)
			
	else: if (BTree != null):
		BTree.tick()
		

func DoAlarmVisual() -> void:
	Alarmed.emit()

func LaunchMissile(Mis : Array[MissileItem], Pos : Vector2, alt : float) -> void:
	var MissileScene : PackedScene = ResourceLoader.load(Mis[0].MissileFile)
	var missile = MissileScene.instantiate() as Missile
	missile.RegisterOwner(self)
	missile.SetData(Mis)
	missile.DisiredAltitude = alt
	if (Command != null):
		missile.global_position = Command.global_position
		missile.Altitude = Command.Altitude
	else:
		missile.global_position = global_position
		missile.Altitude = Altitude
	get_parent().add_child(missile)
	
	missile.look_at(Pos)
	missile.ShipMet.connect(Map.GetInstance().EnemyMet)
	
	Reloading = 2

func Steer(Rotation : float) -> void:
	while rotation_degrees > 180:
		rotation -= 180
	while rotation_degrees < -180:
		rotation += 180
	rotation += Rotation
	
	var Mat = ShipSprite.material as ShaderMaterial
	Mat.set_shader_parameter("sprite_rotation", ShipSprite.global_rotation)

	for g in GetDock().GetDockedShips():
		g.ForceSteer(rotation)

func GetFuelRange() -> float:
	var Weight = Cpt.GetStatFinalValue(STAT_CONST.STATS.WEIGHT)
	var fuel = Cpt.GetStatCurrentValue(STAT_CONST.STATS.FUEL_TANK)
	var fuel_ef = Cpt.GetStatFinalValue(STAT_CONST.STATS.FUEL_EFFICIENCY)
	
	var docked := GetDock().GetDockedShips()
	
	var fleetsize = 1 + docked.size()
	var total_fuel = fuel
	var inverse_ef_sum = 1.0 / ((fuel_ef / pow(Weight, 0.5)) * 10)
	
	# Group ships fuel and efficiency calculations
	for g in docked:
		var ship_fuel = g.Cpt.GetStatCurrentValue(STAT_CONST.STATS.FUEL_TANK)
		var ship_efficiency = g.Cpt.GetStatFinalValue(STAT_CONST.STATS.FUEL_EFFICIENCY)
		var ship_weight = g.Cpt.GetStatFinalValue(STAT_CONST.STATS.WEIGHT)
		total_fuel += ship_fuel
		inverse_ef_sum += 1.0 / ((ship_efficiency / pow(ship_weight, 0.5)) * 10)

	var effective_efficiency = fleetsize / inverse_ef_sum
	# Calculate average efficiency for the group
	return total_fuel * effective_efficiency / fleetsize

func IsFuelFull() -> bool:
	for g in GetDock().GetDockedShips():
		if (!g.IsFuelFull()):
			return false
	return Cpt.IsResourceFull(STAT_CONST.STATS.FUEL_TANK)

func NeedsReload() -> bool:
	for g in GetDock().GetDockedShips():
		if (g.NeedsReload()):
			return true
	return !Cpt.IsResourceFull(STAT_CONST.STATS.MISSILE_SPACE)

func TogglePause(_t : bool):
	pass
	
#func TogglePause(t : bool):
	#Paused = t
	#if (t and BTree != null):
		#BTree.process_mode = Node.PROCESS_MODE_DISABLED
	#else: if (BTree != null):
		#BTree.process_mode = Node.PROCESS_MODE_PAUSABLE

func ToggleDocked(t : bool) -> void:
	super(t)
	if (BTree != null):
		BTree.enabled = !t
		BTree.set_physics_process(!t)

#///////////////////////////////////////////////////
#██████  ███████ ███████ ████████ ██ ███    ██  █████  ████████ ██  ██████  ███    ██     ███    ███  █████  ███    ██  █████   ██████  ███    ███ ███████ ███    ██ ████████ 
#██   ██ ██      ██         ██    ██ ████   ██ ██   ██    ██    ██ ██    ██ ████   ██     ████  ████ ██   ██ ████   ██ ██   ██ ██       ████  ████ ██      ████   ██    ██    
#██   ██ █████   ███████    ██    ██ ██ ██  ██ ███████    ██    ██ ██    ██ ██ ██  ██     ██ ████ ██ ███████ ██ ██  ██ ███████ ██   ███ ██ ████ ██ █████   ██ ██  ██    ██    
#██   ██ ██           ██    ██    ██ ██  ██ ██ ██   ██    ██    ██ ██    ██ ██  ██ ██     ██  ██  ██ ██   ██ ██  ██ ██ ██   ██ ██    ██ ██  ██  ██ ██      ██  ██ ██    ██    
#██████  ███████ ███████    ██    ██ ██   ████ ██   ██    ██    ██  ██████  ██   ████     ██      ██ ██   ██ ██   ████ ██   ██  ██████  ██      ██ ███████ ██   ████    ██    

#Path to destination City
var Path : Array = []
#Current Stage of the path
var PathPart : int = 0

var PursuitPath : Array = []
var PursuitPathPart : int = 0

func SetPositionToInvestigate(Pos : Vector2) -> void:
	PositionToInvestigate = Pos
	if (Pos != Vector2(0,0)):
		if (!CanReachPosition(Pos)):
			FindPursuitPath(Pos)

func SetPursuitTarget(Target : MapShip) -> void:
	PursuingShips.append(Target)
	var Pos = Target.global_position
	if (!CanReachPosition(Pos)):
		FindPursuitPath(Pos)

#Crates a path to the destination city
func FigureOutPath() -> void:
	var cities = get_tree().get_nodes_in_group("EnemyDestinations")
	var nextcity = cities.find(CurrentPort) + Direction
	if (nextcity < 0 or nextcity > cities.size() - 1):
		Direction *= -1
		nextcity = cities.find(CurrentPort) + Direction
	
	#If path is full it means we are loading so skip path generation
	if (Path.size() == 0 and CurrentPort != null):
		var port = CurrentPort
		
		if (CurrentPort.NeighboringCities.size() == 0):
			await Map.GetInstance().MAP_NeighborsSet
			
		Path = Helper.FindPath(port.GetSpotName(), cities[nextcity].GetSpotName())
		if (Path.size() == 0):
			Path = Helper.FindPath(port.GetSpotName(), cities[nextcity].GetSpotName())
		PathPart = 1
	
	BTree = BT.instantiate() as BeehaveTree
	#TODO Test different tickrate on android
	BBoard = Blackboard.new()
	add_child(BBoard)
	BBoard.set_value("TickRate", 1)
	BTree.blackboard = BBoard
	ToggleDocked(Docked)
	add_child(BTree)
	#if (OS.get_name() == "Android"):
		#BTree.tick_rate = 10
	BBoard.set_value("TickRate", 1)

func CanReachDestination() -> bool:
	var dist = GetFuelRange()
	var actualdistance = global_position.distance_to(GetCurrentDestination())
	return dist >= actualdistance

func CanReachPosition(Pos : Vector2) -> bool:
	var dist = GetFuelRange()
	var actualdistance = global_position.distance_to(Pos)
	return dist >= actualdistance

func ToFarFromRefuel() -> bool:
	var dist = GetFuelRange()
	#var DistanceToDestination = global_position.distance_to(GetCurrentDestination())
	for g in get_tree().get_nodes_in_group("EnemyDestinations"):
		var spot = g as MapSpot
		if (spot.global_position.distance_to(global_position) < dist):
			return false
	return true

func SetNewDestination(DistName : String) -> void:
	Path = Helper.FindPath(CurrentPort.GetSpotName(), DistName)
	PathPart = 1

func FindPursuitPath(Pos : Vector2) -> void:
	var ClosestToPosition = Helper.GetClosestSpot(Pos)
	var Closest : MapSpot
	if (CurrentPort != null):
		Closest = CurrentPort
	else:
		Closest = Helper.GetClosestSpot(global_position)
	Path = Helper.FindPath(Closest.GetSpotName(), ClosestToPosition.GetSpotName())
	print("{0} has created a pursuit path from {1} to {2}".format([GetShipName(), Closest.GetSpotName(), ClosestToPosition.GetSpotName()]))
	PathPart = 1

func SetCurrentPort(P : MapSpot) -> void:
	CurrentPort = P
	for g in GetDock().GetDockedShips():
		g.SetCurrentPort(P)

func RemovePort():
	if (Docked):
		return
	#if (CurrentPort == RefuelSpot):
		#RefuelSpot = null
	CurrentPort = null
	for g in GetDock().GetDockedShips():
		g.CurrentPort = null

func IntersectPusruing() -> Vector2:
	var pursuing_ship_position = PursuingShips[0].global_position
	var pursuing_ship_velocity = PursuingShips[0].GetShipSpeedVec()
	var pursuing_ship_speed = PursuingShips[0].GetShipSpeed() / 360
	
	var Distance = global_position.distance_to(pursuing_ship_position)
	
	if (Distance < 10):
		OnReachedPursuing()
	
	if (pursuing_ship_speed == 0):
		return pursuing_ship_position
	
	var DirToPrey = global_position.direction_to(pursuing_ship_position)
	var speed = GetShipMaxSpeed() / 360
	
	var time_to_interception = global_position.distance_to(pursuing_ship_position) / speed
	
	var magnitude = 1
	magnitude += (pursuing_ship_velocity.normalized().dot(DirToPrey) + 1) * 2
	magnitude *= (speed / pursuing_ship_speed)

	var PositionInFuture = pursuing_ship_position + pursuing_ship_velocity * (time_to_interception * magnitude)
	
	var OffsetToPositionInFuture = (PositionInFuture - pursuing_ship_position) / 10
	
	var BestInterceptionPoint : Vector2
	#var TimeToPoint : float = 99999999999
	var TimeOffset : float = 99999999

	for g in range(10, 1, -1):
		var pos = pursuing_ship_position + (OffsetToPositionInFuture * g)

		var TimeToPos = global_position.distance_to(pos) / speed
		var PreyTime = pursuing_ship_position.distance_to(pos) / pursuing_ship_speed
		
		if (abs(TimeToPos - PreyTime) > TimeOffset):
			continue

		BestInterceptionPoint = pos
		#TimeToPoint = TimeToPos
		TimeOffset = abs(TimeToPos - PreyTime)

	return BestInterceptionPoint


func OnReachedPursuing() -> void:
	var plships : Array[MapShip] = []
	var hostships : Array[MapShip] = []
	if (Docked):
		hostships.append(Command)
		hostships.append_array(Command.GetDock().GetDockedShips())
	else:
		hostships.append(self)
		hostships.append_array(GetDock().GetDockedShips())

	var Ship : MapShip = PursuingShips[0]
	if (Ship.Command == null):
		plships.append(Ship)
		for g in Ship.GetDock().GetDockedShips():
			if (g is HostileShip):
				hostships.append(g)
			else:
				plships.append(g)
	else:
		var FleetCommander = Ship.Command
		plships.append(FleetCommander)
		for g in FleetCommander.GetDock().GetDockedShips():
			if (g is HostileShip):
				hostships.append(g)
			else:
				plships.append(g)
	
	var Mis : Array[Missile] = []
	
	OnPlayerShipMet.emit(plships, hostships, Mis, Mis)

func GetCurrentDestination() -> Vector2:
	var destination : Vector2
	#if (RefuelSpot != null):
		#destination = RefuelSpot.global_position
	if (PursuingShips.size() > 0):
		destination = IntersectPusruing()
	else : if(PositionToInvestigate != Vector2.ZERO):
		if (PursuitPath.size() - 1 > PursuitPathPart):
			destination = Helper.GetCityByName(PursuitPath[PursuitPathPart]).global_position
		else:
			destination = PositionToInvestigate
			if (PositionToInvestigate.distance_squared_to(global_position) <= 20):
				OnPositionInvestigated.emit(PositionToInvestigate)
	else: if(RefugeSpot != null) :
		destination = RefugeSpot.global_position
	else : if (Path.size() > 0):
		destination = Helper.GetCityByName(Path[PathPart]).global_position
	else : 
		destination = global_position
	return destination
#/////////////////////////////////////////////////////
#██████  ██   ██ ██    ██ ███████ ██  ██████ ███████     ███████ ██    ██ ███████ ███    ██ ████████ ███████ 
#██   ██ ██   ██  ██  ██  ██      ██ ██      ██          ██      ██    ██ ██      ████   ██    ██    ██      
#██████  ███████   ████   ███████ ██ ██      ███████     █████   ██    ██ █████   ██ ██  ██    ██    ███████ 
#██      ██   ██    ██         ██ ██ ██           ██     ██       ██  ██  ██      ██  ██ ██    ██         ██ 
#██      ██   ██    ██    ███████ ██  ██████ ███████     ███████   ████   ███████ ██   ████    ██    ███████ 
#Overriding events from MapShip as extra functionality is needed for enemies to let know of the COMMANDER of what was found/lost

func OnShipSeen(SeenBy : Node2D) -> void:
	if (Docked):
		Command.OnShipSeen(SeenBy)
		return
	if (VisibleBy.has(SeenBy)):
		return
	VisibleBy.append(SeenBy)
	if (VisibleBy.size() > 1):
		return
	
	MapPointerManager.GetInstance().AddShip(self, false, true)
	for g : HostileShip in GetDock().GetDockedShips():
		g.VisibleBy.append(SeenBy)
		MapPointerManager.GetInstance().AddShip(g, false)
		
	SimulationManager.GetInstance().SpeedToggle(false)
	
	Map.GetInstance().GetCamera().FrameCamToPos(global_position,1, false)

func wait(seconds : float) -> Signal:
	return get_tree().create_timer(seconds).timeout

func OnShipUnseen(UnSeenBy : Node2D) -> void:
	if (Docked):
		Command.OnShipUnseen(UnSeenBy)
		return
		
	VisibleBy.erase(UnSeenBy)
	for g : HostileShip in GetDock().GetDockedShips():
		g.VisibleBy.erase(UnSeenBy)
		
	#if (VisibleBy.size() == 0):
		#ExposedValue = 0
	#$Radar/Radar_Range.visible = VisibleBt.size() > 0


func BodyEnteredBody(Body : Area2D) -> void:
	if (Captured):
		return
	if (Body.get_parent() is MapSpot):
		if (Docked):
			return
		var spot = Body.get_parent() as MapSpot
		SetCurrentPort(spot)
		spot.OnSpotAproached(self)
		for g in GetDock().GetDockedShips():
			SetCurrentPort(spot)
			spot.OnSpotAproached(g)
		if (Path.has(spot.GetSpotName())):
			PathPart = Path.find(spot.GetSpotName())
			if (PathPart == Path.size() - 1):
				OnDestinationReached.emit(self)
			else :
				PathPart += 1
		if (PursuitPath.has(spot.GetSpotName())):
			if (PursuitPathPart < PursuitPath.size() - 1):
				PathPart += 1

	else :if (Body.get_parent() is PlayerDrivenShip):
		if (Destroyed):
			var Wonfunds = Cpt.ProvidingFunds
			World.GetInstance().PlayerWallet.AddFunds(Wonfunds)
			PopUpManager.GetInstance().DoFadeNotif("{0} drahma added".format([Wonfunds]))
			call_deferred("DestroyEnemyDebry")
		#TODO expand logic to allow for convoys with guards
		else : if (Convoy):
			Evaporate()
			World.GetInstance().PlayerWallet.AddFunds(Cpt.ProvidingFunds)
			PopUpManager.GetInstance().DoFadeNotif("Convoy Plundered\n{0} Drahma added".format([Cpt.ProvidingFunds]))
	
func BodyLeftBody(Body : Area2D) -> void:
	if (Body.get_parent() == CurrentPort):
		if (!Docked):
			CurrentPort.OnSpotDeparture(self)
			for g in GetDock().GetDockedShips():
				CurrentPort.OnSpotDeparture(g)
			RemovePort()
			
	#if (Body.get_parent() is PlayerShip or Body.get_parent() is Drone):
		#OnShipUnseen(Body.get_parent())

#//////////////////////////////////////////////////////
 #██████  ███████ ████████ ████████ ███████ ██████  ███████ 
#██       ██         ██       ██    ██      ██   ██ ██      
#██   ███ █████      ██       ██    █████   ██████  ███████ 
#██    ██ ██         ██       ██    ██      ██   ██      ██ 
 #██████  ███████    ██       ██    ███████ ██   ██ ███████ 

func GetBattleStats() -> BattleShipStats:
	var stats = BattleShipStats.new()
	stats.Hull = Cpt.GetStatFinalValue(STAT_CONST.STATS.HULL)
	stats.CurrentHull = Cpt.GetStatCurrentValue(STAT_CONST.STATS.HULL)
	stats.FirePower = Cpt.GetStatFinalValue(STAT_CONST.STATS.FIREPOWER)
	stats.Speed = (Cpt.GetStatFinalValue(STAT_CONST.STATS.THRUST) * 1000) / Cpt.GetStatFinalValue(STAT_CONST.STATS.WEIGHT)
	stats.ShipIcon = ResourceLoader.load(Cpt.ShipIconFile)
	for g in Cpt.CardFightIconsFiles:
		stats.cardFightIcons.append(ResourceLoader.load(g))
	stats.CaptainIcon = Cpt.CaptainPortrait
	stats.Name = GetShipName()
	stats.Funds = Cpt.GetValue()
	stats.Weight = Cpt.GetStatFinalValue(STAT_CONST.STATS.WEIGHT)
	stats.MaxShield =  Cpt.GetStatFinalValue(STAT_CONST.STATS.MAX_SHIELD)
	UpdateShipWindManipulationModifier()
	stats.WindPenalty = GetWindPenalty()
	stats.Convoy = Convoy
	stats.Cards = Cpt.GetCardList()
	stats.Friendly = false
	return stats
	
func GetShipName() -> String:
	return ShipName

#/////////////////////////////////////////////////////////////
#███████  █████  ██    ██ ███████     ██ ██       ██████   █████  ██████  
#██      ██   ██ ██    ██ ██         ██  ██      ██    ██ ██   ██ ██   ██ 
#███████ ███████ ██    ██ █████     ██   ██      ██    ██ ███████ ██   ██ 
	 #██ ██   ██  ██  ██  ██       ██    ██      ██    ██ ██   ██ ██   ██ 
#███████ ██   ██   ████   ███████ ██     ███████  ██████  ██   ██ ██████ 

func GetSaveData() -> SD_HostileShip:
	var dat = SD_HostileShip.new()
	dat.Path = Path
	dat.PathPart = PathPart
	dat.Direction = Direction
	dat.Position = global_position
	dat.Cpt = Cpt
	dat.Scene = scene_file_path
	dat.Patrol = Patrol
	dat.Convoy = Convoy
	dat.ShipName = ShipName
	dat.Destroyed = Destroyed
	if (Command != null):
		dat.CommandName = Command.GetShipName()
	#dat.WeaponInventory = WeaponInventory
	return dat
	
func LoadSaveData(Dat : SD_HostileShip) -> void:
	#DestinationCity = GetCity(Dat.DestinationCityName)
	Path = Dat.Path
	PathPart = Dat.PathPart
	Direction = Dat.Direction
	Patrol = Dat.Patrol
	Convoy = Dat.Convoy
	#positioning happens on script wich respawns ship
	#global_position = Dat.Position
	
	Cpt = Dat.Cpt
	ShipName = Dat.ShipName
	Destroyed = Dat.Destroyed
	LoadingSave = true
	#WeaponInventory = Dat.WeaponInventory

#///////////////////////////////////////////////////
#██████   █████  ███    ███  █████   ██████  ██ ███    ██  ██████  
#██   ██ ██   ██ ████  ████ ██   ██ ██       ██ ████   ██ ██       
#██   ██ ███████ ██ ████ ██ ███████ ██   ███ ██ ██ ██  ██ ██   ███ 
#██   ██ ██   ██ ██  ██  ██ ██   ██ ██    ██ ██ ██  ██ ██ ██    ██ 
#██████  ██   ██ ██      ██ ██   ██  ██████  ██ ██   ████  ██████  

#When Killed outside of battle (With missile) enemies will leave behind wreck, then this bool will be true
var Destroyed : bool

func IsDamaged() -> bool:
	for g in GetDock().GetDockedShips():
		if (g.IsDamaged()):
			return true
	return !Cpt.IsResourceFull(STAT_CONST.STATS.HULL)

func Evaporate() -> void:
	OnShipDestroyed.emit(self)
	Destroyed = true
	if (ElintShape != null):
		ToggleElint(false)
	if (CurrentPort != null):
		CurrentPort.OnSpotDeparture(self)
	ToggleRadar(false)
	MapPointerManager.GetInstance().RemoveShip(self)
	queue_free()
	#get_parent().remove_child(self)
	
func Kill() -> void:
	OnShipDestroyed.emit(self)
	Destroyed = true
	if (ElintShape != null):
		ToggleElint(false)
		
	ToggleRadar(false)
	ShipWrecked.emit()
	if (CurrentPort != null):
		CurrentPort.OnSpotDeparture(self)
	
func DestroyEnemyDebry() -> void:
	MapPointerManager.GetInstance().RemoveShip(self)
	get_parent().remove_child(self)
	queue_free()
	
